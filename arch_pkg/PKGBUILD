# Maintainer : Nevar <psi.neamf@gmail.com>
# Contributor: Gary van der Merwe <garyvdm@gmail.com>

pkgname=pacshare
pkgver=20120113
pkgrel=1
pkgdesc="Share pacman packages on the LAN."
arch=('i686' 'x86_64')
url="https://github.com/psi-neamf/pacshare"
license=('GPL')
makedepends=('python-distribute')
depends=('avahi' 'python' 'python-progressbar' 'python-webob'
         'pyalpm' 'python-dbus' 'dbus-glib' 
         'python-gobject2' 'python-waitress')
install=pacshare.install
source=("pacshare-init"
        "pacshare-init.conf"
        "pacshare-logrotate"
        "pacshare.install")
sha1sums=('debf45f69a41df0e2305610b627609846d0aa267'
          'd294f1ab271fc7e9d8f904568a50136c86fe4eb3'
          '15331ed5b81c8165be28311571503def8c23c923'
          '5f17e4f70a8ca6d1591a5b19dc4db88c0b03b298')

_gitroot="git://github.com/psi-neamf/pacshare.git"
_gitname="pacshare"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

  cd "$srcdir/$_gitname-build"
  python setup.py build
}

package() {
  cd $srcdir/$_gitname-build
  python setup.py install --root=${pkgdir} --install-scripts="/usr/lib"

  install -Dp -m0755 ${startdir}/pacshare-init ${pkgdir}/etc/rc.d/pacshare
  install -Dp -m0644 ${startdir}/pacshare-init.conf ${pkgdir}/etc/conf.d/pacshare.conf
  install -Dp -m0644 ${startdir}/pacshare-logrotate ${pkgdir}/etc/logrotate.d/pacshare
}

# vim:set ts=2 sw=2 et:

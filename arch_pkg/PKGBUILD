pkgname=pacshare
pkgver=r59.78a1907
pkgrel=1
pkgdesc="Share pacman packages on the LAN."
arch=('any')
url="https://github.com/psi-neamf/pacshare"
license=('GPL')
makedepends=('python-distribute')
depends=('avahi' 'python' 'python-progressbar' 'python-webob'
         'pyalpm' 'python-dbus' 'dbus-glib' 
         'python-gobject2' 'python-waitress')
install=pacshare.install
source=('git+https://github.com/v01d-cypher/pacshare.git'
        'pacshare.service'
        'pacshare.install')
sha1sums=('SKIP'
          '3b1540bf6143dbe441afc09e1d8c7a49a14437fe'
          '4418809cf929b72ca3a6b683670180ca945e0810')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"
  python setup.py build
}

package() {
  cd $srcdir/$pkgname
  python setup.py install --root=${pkgdir} --install-scripts="/usr/lib"

  install -Dp -m0755 ${startdir}/pacshare.service ${pkgdir}/usr/lib/systemd/system/pacshare.service
}

# vim:set ts=2 sw=2 et:
